plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

// Основная информация
group = 'com.example'
archivesBaseName = 'telegram-bot'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

// Кодировка
tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

def loadEnvFile() {
    def env = [:]
    def envFile = file('.env')
    if (envFile.exists()) {
        println "Загружаю переменные из .env"
        envFile.eachLine { line ->
            if (line && !line.startsWith('#') && line.trim().contains('=')) {
                def (key, value) = line.split('=', 2)*.trim()
                value = value.replaceAll(/^"(.*)"$/, '$1').replaceAll(/^'(.*)'$/, '$1')
                env[key] = value
                logger.quiet("ENV: $key = ${'*'.repeat(value.length())}")
            }
        }
    } else {
        logger.warn("Файл .env не найден — ожидаем переменные окружения извне (например, Docker)")
    }
    return env
}

def envVars = loadEnvFile()

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.aliyun.com/repository/public' }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'ch.qos.logback:logback-classic:1.4.14'
    implementation 'com.github.pengrad:java-telegram-bot-api:6.9.0'
    implementation 'com.google.guava:guava:32.0.1-android'
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
}

test {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'UTF-8'
    jvmArgs '-Xshare:off'
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
        csv.required = false
    }
}

run {
    standardInput = System.in
    environment envVars
}

application {
    mainClass.set('AppMain')
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'AppMain'
    }
}

jar.enabled = false
build.finalizedBy(shadowJar)