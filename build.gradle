plugins {
    id 'java'
    id 'application'
    id 'jacoco'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

archivesBaseName = 'telegram-bot'
version = '1.0.0'

repositories {
    maven { url 'https://maven.aliyun.com/repository/public' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.pengrad:java-telegram-bot-api:6.9.0'

    implementation 'com.google.guava:guava:32.0.1-android'
    implementation 'com.fasterxml.uuid:java-uuid-generator:4.0.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.11.0'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

test {
    useJUnitPlatform()
    systemProperty 'file.encoding', 'UTF-8'
    jvmArgs '-Xshare:off'
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('run') {
    standardInput = System.in
}

application {
    mainClass.set('App')
}

shadowJar {
    archiveClassifier.set('')
    manifest {
        attributes 'Main-Class': 'App'
    }
}

jar.enabled = false
build.finalizedBy(shadowJar)